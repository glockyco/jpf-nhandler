apply plugin: "java"

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
    ant.importBuild 'build-properties.xml'

    // jpf-core + jars used in the examples
    implementation files(ant.properties['jpf-core.native_classpath'].split(';'))

    // JUnit 4
    testImplementation 'junit:junit:4.12'

    // bcel
    implementation 'org.apache.bcel:bcel:6.5.0'

    // jars used in the examples
    implementation fileTree(dir: 'lib/example', include: ['*/*.jar'])
}

compileJava {
    dependsOn(':jpf-core:buildJars')
}

jar {
    archiveBaseName = 'jpf-nhandler'
    exclude('example/**')   
    destinationDirectory.set(file("${buildDir}"))
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }

    jvmArgs += ['--add-exports', 'java.base/jdk.internal.misc=ALL-UNNAMED', '--add-opens', 'java.base/jdk.internal.misc=ALL-UNNAMED', '--add-opens', 'java.base/java.lang=ALL-UNNAMED', '--add-opens', 'java.base/java.util=ALL-UNNAMED']
    
    afterSuite { testDescriptor, result ->
        if (!testDescriptor.parent) {
            println "Test Execution: ${result.resultType}"

            def summaryFields = ["${result.testCount} tests",
                                 "${result.successfulTestCount} passed",
                                 "${result.failedTestCount} failed",
                                 "${result.skippedTestCount} skipped"]

            println ("Summary: " + summaryFields.join(", "))
        }
    }
}

defaultTasks "jar"
